*
*  FILE:    WDS1.158.EFKINT
*
*  NAME:    EXTENDED FUNCTION KEY SUPPORT
*
*  VERSION: 3.0 - 10/19/86    BASE LINE
*
*

FKINT8 DECT R10               * MAKE ROOM
       MOV  R11,*R10          * SAVE RETURN
       LI   R0,SIDGOS*256     * LOAD THE STACK MARK
       BL   @BLDMRK           * BUILD A STACK ENTRY
       MOV  *R10+,R11         * GET RETURN BACK
       SETO @10(R9)           * INDICATE ITS A FUNC/MSE
       INC  @14(R9)           * TO RESTORE PGM POINTER

       MOV  @CLINE,@06+FAC    * SAVE THE CURRENT LINE NUMBER

       BLWP @SRCHLN           * SEARCH LINE NUMBER TABLE
       MOV  @CLINE,R0         * CHECK IF WE FOUND IT
       JEQ  FKINT7            * JUMP IF NOT FOUND
       CLR  R0                * RETURN R0=0
       RT

       PAGE
*
*    NAME:  FUNCTION KEY INTERRUTPT
*
*    FUNCTION KEY INTERRUPT. THE FLAG WAS SET. SOME
*    FUNCTION KEY NUMBER IS ARMED.
*    READ THE KEYBOARD AND CKECK IF THE KEY IS A FUNCTION KEY
*    IF YES, TRY AND PROCESS IT
*    IF NO,  ADD TO CLIST

EFKINT MOV  @KEYBD,R0         * GET KEYBOARD #
       XOP  @FIVE,0
       JNE  FKINT1            * JUMP IF NO KEY RETURNED

       CB   R1,@SPACE         * CHECK IF FUNCTION KEY
       JL   FKINT3            * JUMP IF YES
FKINT5 MOV  @CLLEN,R3         * GET INDEX INTO BUFFER
       CI   R3,14             * CHECK IF BUFFER FULL
       JHE  FKINT1            * JUMP IF YES
       MOVB R1,@CLIST(R3)     * SAVE CHARACTER
       INC  @CLLEN            * ADD ONE TO LENGTH
FKINT1 SETO R0                * RETURN, NO KEY FOUND
       RT

FKINT3 MOV  R1,R2             * GET A COPY OF R1
       SRA  R2,8              * ALIGN WORD
       SLA  R2,1              * MAKE INTO INDEX
       MOV  @FTBL(R2),R2      * READ FUNCTION KEY NUMBER/INDEX

*    TEST ON/OFF FLAG, AND STOP FLAG

       MOV  @FKKEY(R2),R0     * GET BOTH FLAGS
       JEQ  FKINT1            * JUMP IF OFF

       COC  @CW01,R0          * TEST STOP FLAG
       JEQ  FKINT5            * DONT PROCESS KEY

       INCT R2                * POINT TO LINE NUMBER
       MOV  @FKKEY(R2),@SNUM+FAC
       JEQ  FKINT1            * JUMP IF ZERO NO LINE NUMBER

       CLR  @CLLEN            * FLUSH CHAR LIST BUFFER
       JMP  FKINT8            * USE COMMON CODE


FKINT7 AI   R9,-STKSZ         * POP THE STACK
       MOV  @06+FAC,@CLINE    * RESTORE THE CURRENT LINE NUMBER

       LI   R0,ERRLNF
       BL   @ERROR

FKINT9 BL   @RPTE03           * SYNTAX ERROR

*    KEY ENTRY TABLE IS HERE. ONE ENTRY FOR EACH
*    KEY UNDER 16. INVALID KEY CODES GO TO READ$1

*           FK16 FK7  FK4  FK1
FTBL   DATA 0060,0024,0012,0000     FK16 NOT ACTIVE
*           FK2  FK15 FK8  FK3      FK15 SWAPPED WITH FK16
       DATA 0004,0056,0028,0008
*           FK10 FK11 FK12 FK13
       DATA 0036,0040,0044,0048
*           FK6  FK14 FK5  FK9
       DATA 0020,0052,0016,0032


